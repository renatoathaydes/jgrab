import java.util.regex.Pattern

apply plugin: 'idea'

allprojects {
    group = 'com.athaydes.jgrab'
    version = '1.0'
}

task localRelease( dependsOn: ':jgrab-runner:localInstall' ) {
    def findJGrabClientVersion = { ->
        def inPackageSection = false
        def versionPattern = Pattern.compile( /\s*version\s*=\s*"(.+)"\s*/ )
        def version = 'UNKNOWN'
        file( 'jgrab-client/Cargo.toml' ).readLines().find { line ->
            if ( inPackageSection ) {
                def matcher = line =~ versionPattern
                if ( matcher.matches() ) {
                    version = matcher.group( 1 )
                    return true // stop searching
                }
            } else if ( line == '[package]' ) {
                inPackageSection = true
            }
            false
        }
        return version
    }

    doLast {
        def clientVersion = findJGrabClientVersion()
        exec {
            workingDir file( 'jgrab-client' )
            commandLine 'cargo', 'build', '--release'
        }
        exec {
            workingDir file( 'jgrab-client' )
            commandLine 'strip', 'target/release/jgrab-client'
        }
        copy {
            from file( 'jgrab-client/target/release/jgrab-client' )
            into file( 'releases' )
            rename '.*', "jgrab-client-${clientVersion}"
        }

        println "Local Release of JGrab $version and Client $clientVersion complete."
    }
}
