plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 11
targetCompatibility = 11

description = 'Runs Java code without a build system, grabbing dependencies declared in the Java file itself.'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.athaydes.jbuild:jbuild:0.8.0'
    implementation 'com.athaydes.osgiaas:osgiaas-javac:0.7'
    implementation 'org.slf4j:slf4j-simple:1.7.21'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

tasks.register( 'fatJar', Jar ) {
    manifest {
        attributes 'Implementation-Title': 'JGrab',
                'Implementation-Version': version,
                'Main-Class': 'com.athaydes.jgrab.runner.JGrabRunner'
    }
    archiveFileName = 'jgrab.jar'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree( it ) } }
    with jar
}

test {
    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
}

tasks.register( 'localInstall', Copy ) {
    dependsOn 'fatJar'
    from fatJar, {
        rename '.*', 'jgrab.jar'
    }
    into "${System.getProperty( 'user.home' )}/.jgrab/"
}

/* Publishing config */

jar {
    manifest {
        attributes(
                "Implementation-Title": "JGrab",
                "Implementation-Version": project.version,
                "Automatic-Module-Name": 'com.athaydes.jgrab' )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if ( JavaVersion.current().isJava9Compatible() ) {
        options.addBooleanOption( 'html5', true )
    }
}

artifacts {
    archives fatJar
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            artifactId = 'jgrab'
            from components.java
            versionMapping {
                usage( 'java-api' ) {
                    fromResolutionOf( 'runtimeClasspath' )
                }
                usage( 'java-runtime' ) {
                    fromResolutionResult()
                }
            }
            pom {
                inceptionYear = '2017'
                name = project.name
                packaging = 'jar'
                description = project.description

                url = 'https://github.com/renatoathaydes/jgrab'

                scm {
                    connection = 'git@github.com:renatoathaydes/jgrab.git'
                    developerConnection = 'git@github.com:renatoathaydes/jgrab.git'
                    url = 'https://github.com/renatoathaydes/jgrab'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'renatoathaydes'
                        name = 'Renato Athaydes'
                        email = 'renato@athaydes.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = project.getProperties().get( 'ossrhUsername', '' )
                password = project.getProperties().get( 'ossrhPassword', '' )
            }
        }
    }
}

signing {
    if ( project.hasProperty( 'sign' ) ) {
        sign publishing.publications.mavenJava
    }
}
